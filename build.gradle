plugins {
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
}

subprojects { ->
    project
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven'
    apply plugin: 'signing'
    group 'com.github.faster-framework'
    sourceCompatibility = 1.8
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    }
    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-starter-parent:2.0.2.RELEASE'
        }
    }
    processResources {
        from('src/main/java') {
            include '**/*.xml'
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
    }
    if (version =~ 'RELEASE$') {
        signing {
            sign configurations.archives
        }
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }

                    pom.project {
                        name project.name
                        packaging 'jar'
                        description 'faster framework by Spring Boot'
                        url 'https://github.com/faster-framework/'

                        scm {
                            url 'https://github.com/faster-framework/'
                            connection 'https://github.com/faster-framework/'
                            developerConnection 'https://github.com/faster-framework/'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id "zhangbowen";
                                name "Zhang Bowen";
                                email "515445681@qq.com";
                            }
                        }
                    }
                }
            }
        }
    }
}


